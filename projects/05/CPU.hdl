// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15], out=notmsb);
    Mux16(a=outalu, b=instruction, sel=notmsb, out=ina);

    And(a=instruction[15], b=instruction[5], out=loadatmp);
    Or(a=notmsb, b=loadatmp, out=loada);

    ARegister(in=ina, load=loada, out=outa);

    // Check that it is a C-instr and the bit is set
    And(a=instruction[15], b=instruction[4], out=loadd);
    DRegister(in=outalu, load=loadd, out=outd);

    And(a=instruction[15], b=instruction[12], out=aorm);
    Mux16(a=outa, b=inM, sel=aorm, out=outam);

    // if it's a C-instruction and the writeM bit is set
    And(a=instruction[15], b=instruction[3], out=writeM);
    And16(a=outa, b=true, out[0..14]=addressM);

    ALU(x=outd, y=outam, zx=instruction[11], nx=instruction[10], 
      zy=instruction[9], ny=instruction[8], f=instruction[7],
      no=instruction[6], out=outalu, zr=zr, ng=ng);

    And16(a=outalu, b=true, out=outM);

    // jmp if it is a C-instruction and the following conditions are met
    // * all the dst bits are 0
    // * one of the jmp bits is 1 AND their respective prerequisite is met
    // else inc PC

    Or(a=notmsb, b=instruction[5], out=tmpd1);
    Or(a=instruction[4], b=instruction[3], out=tmpd2);
    Or(a=tmpd1, b=tmpd2, out=nojmp);
    Not(in=nojmp, out=dojmp);

    Not(in=zr, out=notzr);
    Not(in=ng, out=notng);
    
    And(a=instruction[2], b=ng, out=jneg);
    And(a=instruction[1], b=zr, out=jz);
    And(a=notzr, b=notng, out=tmpnot);
    And(a=tmpnot, b=instruction[0], out=jpos);

    Or(a=jz, b=jneg, out=jtmp1);
    Or(a=jtmp1, b=jpos, out=outjmp);

    And(a=dojmp, b=outjmp, out=jmpflag);
    Not(in=jmpflag, out=inc);

    PC(in=outa, load=jmpflag, inc=inc, reset=reset, out[0..14]=pc);


}
